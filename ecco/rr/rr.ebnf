@@grammar :: rr

@@eol_comments :: /#.*?$/
@@whitespace :: /[\t ]+/

start =
    {nl}
    context:{vdecl}+
    rules:{cdecl|rdecl}+
    {nl}$
    ;

vdecl =
    name:word ":" {nl}+
    decl:{varstate ":" description:/.*?$/ {nl}+}+
    ;

varstate =
   variable:word state:/[+-]/
   ;

word =
    /[A-Za-z][A-Za-z0-9_]*/
    ;

nl =
    /\s*[\n\r]\s*/
    ;

cdecl =
    "constraints" ":" {nl}+
    rules:{rule}+
    ;

rdecl =
    "rules" ":" {nl}+
    rules:{rule}+
    ;

rule =
    { "[" label:/[^\]]+/ "]" }?
    ","%{ left:varstate }+ ">>" ","%{ right:varstate }+ {nl}+
    ;

string =
    | "'" { "\\" /./ | /[^\\\r\n\f']+/ } "'"
    | '"' { "\\" /./ | /[^\\\r\n\f"]+/ } '"'
    ;
